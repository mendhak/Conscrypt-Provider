plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'signing'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.mendhak.conscryptprovider"
        minSdk 16
        targetSdk 32
        versionCode 3
        versionName "3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }

    //Release signing key
    if (project.hasProperty("RELEASE_STORE_FILE")) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }

        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'

    implementation 'org.conscrypt:conscrypt-android:2.5.2'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

task copyFinalAPK(group:'build') {
    //Copies final APK to this directory
    def finalApkName = "conscrypt-provider-"+android.defaultConfig.versionName+".apk"

    copy{
        from "build/outputs/apk/release/app-release.apk"
        into "./"

        // Use a closure to map the file name
        rename { String fileName ->
            fileName.replace("app-release.apk", finalApkName)
        }
    }
    //PGP Sign
    //Verify with gpg --verify ~/Downloads/conscrypt-provider-71.apk.asc
    if(file(finalApkName).isFile()){
        signing {
            useGpgCmd()
            sign file(finalApkName)
        }

        //SHA256 Checksum
        //Verify with sha256sum -c ~/Downloads/conscrypt-provider-71.apk.SHA256
        ant.checksum(file: finalApkName, fileext: '.SHA256', algorithm: "SHA-256", pattern: "{0} {1}")
    }

}

task deleteReleaseAPKs(group:"build", type:Delete){
    delete fileTree(dir:'./', include:'*.apk*')
}